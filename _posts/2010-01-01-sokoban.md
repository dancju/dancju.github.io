---
layout: post
title: A Sokoban solver
---

Sokoban is a puzzle game devised in 1981. Vanilla breadth-first search can find the optimum solution to any Sokoban puzzle.

<div style="text-align: center;">
	<img src="https://upload.wikimedia.org/wikipedia/commons/4/4b/Sokoban_ani.gif" alt="Sublime's custom image"/>
	<p>created by Borgar Þorsteinsson</p>
</div>

In this article I am giving my implementation of a sokoban solver, which takes an text file as input, such as

```
..█████.
███...█.
█XMO..█.
███.OX█.
█X██O.█.
█.█.X.██
█O.0OOX█
█...X..█
████████
```

, and produces the optimum solution of the given input. The program is listed as follows.

```pascal
program sokoban;

const
	change: array[0..3,0..1] of shortint=((0,1),(1,0),(0,-1),(-1,0));

var
	free, target: array[-1..128,-1..128] of boolean;
	queue: array[word] of record
		father: word;
		box: array[-1..128,-1..128] of boolean;
		xb, yb: byte;
	end;
	head, tail: word;
	x, y: byte;
	direction: 0..3;

	procedure init;
	var
		i, j: byte;
		get_char: char;
		f: text;
	begin
		assign(f, 'input.txt'); reset(f);
		head:=0; tail:=0; queue[0].father:=65535;
		readln(f, x, y);
		for i:=1 to x do begin
			for j:=1 to y+1 do begin
				read(f, get_char);
				case get_char of
					'.': free[i,j]:=true;
					'M': begin
						free[i,j]:=true;
						queue[0].xb:=i; queue[0].yb:=j;
					end;
					'O': begin
						free[i,j]:=true;
						queue[0].box[i,j]:=true;
					end;
					'0': begin
						free[i,j]:=true;
						queue[0].box[i,j]:=true;
						target[i,j]:=true;
					end;
					'X': begin
						free[i,j]:=true;
						target[i,j]:=true;
					end;
				end;
			end;
			readln(f);
		end;
	end;

	procedure bfs;

		procedure print;
		var i, j: word;
		begin
			for i:=1 to x do begin
				for j:=1 to y do begin
					if not free[i,j] then
						write(' ')
					else if (i = queue[tail].xb) and (j = queue[tail].yb) then
						write('M')
					else if target[i,j] and queue[tail].box[i,j] then
						write('0')
					else if target[i,j] then
						write('X')
					else if queue[tail].box[i,j] then
						write('O')
					else
						write('.');
					end;
				writeln;
				end;
			readln;
		end;

		procedure bingo;
		var
			i, j: byte;
			top: word; stack: array[word] of word;
		begin
			for i:=1 to x do
				for j:=1 to y do
					if queue[tail].box[i,j] <> target[i,j] then
						exit;
			while tail <> 65535 do begin
				print;
				tail:=queue[tail].father;
				end;
			halt;
		end;

		procedure check;
		var k: word;

			function same: boolean;
			var i, j: byte;
			begin
				for i:=1 to x do
					for j:=1 to y do
						if queue[tail].box[i,j] <> queue[k].box[i,j] then
  						exit(false);
				exit(true);
			end;

		begin
			for k:=tail-1 downto 0 do
				if (queue[tail].xb = queue[k].xb) and (queue[tail].yb = queue[k].yb) and same then begin
					dec(tail);
					exit;
				end;
		end;

	begin
		while true do begin
			for direction:=0 to 3 do
				if free[queue[head].xb+change[direction,0],queue[head].yb+change[direction,1]] // 1 free
						and (not queue[head].box[queue[head].xb+change[direction,0],queue[head].yb+change[direction,1]]) then begin // 1 no box
					inc(tail);
					queue[tail]:=queue[head]; queue[tail].father:=head;
					queue[tail].xb:=queue[tail].xb+change[direction,0]; queue[tail].yb:=queue[tail].yb+change[direction,1];
					bingo;
					check;
				end else if free[queue[head].xb+change[direction,0] shl 1,queue[head].yb+change[direction,1] shl 1] // 2 free
						and queue[head].box[queue[head].xb+change[direction,0],queue[head].yb+change[direction,1]] // 1 box
						and (not queue[head].box[queue[head].xb+change[direction,0] shl 1,queue[head].yb+change[direction,1] shl 1]) then begin // 2 no box
					inc(tail);
					queue[tail]:=queue[head];
					queue[tail].father:=head;
					queue[tail].xb:=queue[tail].xb+change[direction,0];
					queue[tail].yb:=queue[tail].yb+change[direction,1];
					queue[tail].box[queue[tail].xb,queue[tail].yb]:=false;
					queue[tail].box[queue[tail].xb+change[direction,0],queue[tail].yb+change[direction,1]]:=true;
					bingo;
					check;
				end;
			inc(head);
		end;
	end;

begin
	init;
	bfs;
end.
```
